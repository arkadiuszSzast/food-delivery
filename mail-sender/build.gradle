plugins {
	id 'org.springframework.boot' version '2.2.6.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'com.palantir.docker' version '0.25.0'
	id "org.sonarqube" version "2.8"
	id 'jacoco'
	id 'java'
}

group = 'com.food.delivery'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '14'

sonarqube {
	properties {
		property "sonar.projectKey", "arkadiuszSzast_food-delivery_mail_sender"
		property "sonar.organization", "arkadiuszszast"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.login", "5aa99f5fa69a40bf14ac90d7114003dadbe50f12"
	}
}

docker {
	name "${project.name}:${project.version}"
	files "build/libs/${project.name}-${project.version}.jar"
	tag 'DockerHub', "szastarek/food-delivery-${project.name}:${project.version}"
}

jacocoTestReport {
	reports {
		xml.enabled true
		csv.enabled false
		html.enabled true
	}

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: '**/MailSenderApplication.class')
		}))
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

task cleanBuildDockerPush(type: GradleBuild) {
	tasks = ['clean', 'build', 'docker', 'dockerTagDockerHub', 'dockerPushDockerHub']
}

ext {
	set('springCloudVersion', "Hoxton.SR3")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.cloud:spring-cloud-starter-config'
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	implementation 'com.playtika.reactivefeign:feign-reactor-spring-configuration:2.0.2'
	implementation 'com.playtika.reactivefeign:feign-reactor-webclient:2.0.2'
	implementation 'com.playtika.reactivefeign:feign-reactor-cloud:2.0.2'
	implementation 'org.springframework.retry:spring-retry'
	implementation 'com.github.arkadiuszSzast:reactive-logstash-logging-starter:1.0.7'
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation 'org.springframework.kafka:spring-kafka'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}
