plugins {
	id 'org.springframework.boot' version '2.2.6.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'com.palantir.docker' version '0.25.0'
	id "org.sonarqube" version "2.8"
	id 'jacoco'
	id 'java'
}

group = 'com.food.delivery'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '14'

sonarqube {
	properties {
		property "sonar.projectKey", "arkadiuszSzast_food-delivery_company_service"
		property "sonar.organization", "arkadiuszszast"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.login", "2a16cba06d99d81ed463772eb226a9abbd1a4b42"
		property 'sonar.coverage.exclusions', "**/FeignClientInterceptor.java," +
				"**/SwaggerConfiguration.java," +
				"**/CompanyServiceApplication.java"
	}
}

docker {
	name "${project.name}:${project.version}"
	files "build/libs/${project.name}-${project.version}.jar"
	tag 'DockerHub', "szastarek/food-delivery-${project.name}:${project.version}"
}

jacocoTestReport {
	reports {
		xml.enabled true
		csv.enabled false
		html.enabled true
	}

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: '**/CompanyServiceApplication.class')
		}))
	}
}

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
	maven { url 'https://jitpack.io' }
}

task cleanBuildDockerPush(type: GradleBuild) {
	tasks = ['clean', 'build', 'docker', 'dockerTagDockerHub', 'dockerPushDockerHub']
}

ext {
	set('springCloudVersion', "Hoxton.SR3")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'com.okta.spring:okta-spring-boot-starter:1.4.0'
	implementation 'org.springframework.cloud:spring-cloud-starter-config'
	implementation 'org.springframework.retry:spring-retry'
	implementation 'com.github.arkadiuszSzast:reactive-logstash-logging-starter:1.0.7'
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	implementation 'com.playtika.reactivefeign:feign-reactor-spring-configuration:2.0.2'
	implementation 'com.playtika.reactivefeign:feign-reactor-webclient:2.0.2'
	implementation 'com.playtika.reactivefeign:feign-reactor-cloud:2.0.2'
	implementation 'io.springfox:springfox-swagger2:3.0.0-SNAPSHOT'
	implementation 'io.springfox:springfox-swagger-ui:3.0.0-SNAPSHOT'
	implementation 'io.springfox:springfox-spring-webflux:3.0.0-SNAPSHOT'
	implementation 'io.micrometer:micrometer-registry-prometheus'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.junit.jupiter:junit-jupiter-api'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'com.playtika.testcontainers:embedded-mongodb:1.66'
	testImplementation 'org.springframework.security:spring-security-test'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}
